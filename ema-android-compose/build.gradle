apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply from: '../build-system/compose.gradle'
apply plugin: 'maven-publish'

android {

    defaultConfig {
        compileSdkVersion emaVersioning.compileSdk
        minSdkVersion emaVersioning.minSdk
        targetSdkVersion emaVersioning.targetSdk

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled = true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
        freeCompilerArgs += [
                "-Xallow-jvm-ir-dependencies",
                "-P",
                "plugin:androidx.compose.compiler.plugins.kotlin:suppressKotlinVersionCompatibilityCheck=true"
        ]
        //useIR = true
    }
    packagingOptions {
        resources {
            pickFirsts += ['META-INF/kotlinx-io.kotlin_module', 'META-INF/ema-android_debug.kotlin_module']
        }
    }


    with(composeConfig)
    namespace 'com.carmabs.ema.compose'
}

dependencies {
    implementation project(path: ':ema-android')
    implementation emaAndroidDependencies.reflection

    api platform(emaComposeDependencies.bom)
    api emaComposeDependencies.ui
    api emaComposeDependencies.tooling
    api emaComposeDependencies.toolingPreview
    api emaComposeDependencies.activity
    api emaComposeDependencies.koin
    api emaComposeDependencies.lifecycle
    api emaComposeDependencies.viewmodel
    api emaComposeDependencies.navigation

    //Java Time API for SDK version < 26
    coreLibraryDesugaring emaAndroidDependencies.desugaring
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = emaVersioning.groupId
                artifactId = emaVersioning.artifactComposeId
                version = emaVersioning.versionName
            }
        }
    }
}
